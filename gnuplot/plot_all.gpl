#!/home/mereghpo/my_bin/gnuplot-4.2
reset;
set terminal postscript landscape enhanced color 15;
#set terminal pngcairo enhanced size 1000, 800
set size 1,1;

# styles
#      line       point  
# -------------------------
# 1 :  cont.       +
# 2 :  ----        x
# 3 :  dashed2     * 
# 4 :  pointed     empty square
# 5 :  dash-point  filled square
# 6 :  dash-point  empty circle
# 7 :  -- -- --    filled circle
# 8 :  -..-..--    empty triangle up
# 9 :  dashed3     filled triangle up
# 10:  cont.       empty triangle down
# 11:  ----        filled triangle down

# Predefined colored line/point styles
set style line 1 lt 1 lw 5.5 pt 1 ps 3.0 lc rgb "#880000" # dark red
set style line 2 lt 2 lw 5.5 pt 2 ps 3.0 lc rgb "#FF0000" # ligh red
set style line 3 lt 3 lw 5.5 pt 3 ps 3.0 lc rgb "#008800" # dark green
set style line 4 lt 4 lw 5.5 pt 4 ps 3.0 lc rgb "#00FF00" # light green
set style line 5 lt 5 lw 5.5 pt 5 ps 3.0 lc rgb "#000088" # dark blue
set style line 6 lt 6 lw 5.5 pt 6 ps 3.0 lc rgb "#0000FF" # light blue
set style line 7 lt 1 lw 5.5 pt 7 ps 3.0 lc rgb "#FFFF00" # light yellow
set style line 8 lt 1 lw 5.5 pt 8 ps 3.0 lc rgb "#FF9900" # orange 
set style line 9 lt 1 lw 5.5 pt 9 ps 3.0 lc rgb "#FF00FF" # pink
set style line 10 lt 1 lw 5.5 pt 10 ps 0.5 lc rgb "#FF9900" # orange 

set style line 11 lt 1 lw 5.5 pt 4 ps 3.0 lc rgb "#000000" # black
set style line 12 lt 2 lw 5.5 pt 7 ps 3.0 lc rgb "#FF0000" # ligh red
set style line 13 lt 2 lw 5.5 pt 5 ps 3.0 lc rgb "#008800" # ligh green

# Predefined B/W

set style line 21 lt 1 lw 5.5 pt 1 ps 3.0 lc rgb "#000000" # black
set style line 22 lt 1 lw 5.5 pt 2 ps 3.0 lc rgb "#000000" # black
set style line 23 lt 1 lw 5.5 pt 3 ps 3.0 lc rgb "#000000" # black
set style line 24 lt 1 lw 5.5 pt 4 ps 3.0 lc rgb "#000000" # black

set style line 25 lt 2 lw 5.5 pt 4 ps 3.0 lc rgb "#008800" # dark green
set style line 26 lt 3 lw 5.5 pt 8 ps 3.0 lc rgb "#000088" # dark blue
set style line 27 lt 4 lw 5.5 pt 8 ps 3.0 lc rgb "#FF9900" # orange
set style line 28 lt 5 lw 5.5 pt 2 ps 3.0 lc rgb "#888800" # yellow

set encoding iso_8859_1 

unset grid;

#hemoglobin radius of gyration [A]
hemo_a=24.1

###################################################################################################
###################################################################################################
# Myoglobin

set size 1.0,1.0
set origin 0.0,0.0
set bmargin 4
set lmargin 12
set rmargin 4
set tmargin 4
set border lw 2

set output 'ps/myoglobin_diffusion.ps';
set multiplot;
unset title;

set xtics nomirror
set ytics nomirror
set xlabel "{/Symbol F}" font "Helvetica Bold,24" offset 0,-1;
set x2label "g/L" font "Helvetica Bold,24" offset 0, +1;
set ylabel "D^{L}_{s} ({/Helvetica=24 \305}^2/ns)" font "Helvetica Bold, 24" offset -2,+0;
set xtics auto font "Helvetica Bold,24";
set x2tics auto font "Helvetica Bold,24";
set x2tics ("0" 0.0, "136" 0.1,"273" 0.2, "410" 0.3, "547" 0.4, "684" 0.5) font "Helvetica Bold,24";
set ytics auto font "Helvetica Bold,24";
set xrange [0.0:0.5];
set x2range [0.0:0.5];
set yrange [0.01:1.1];
set key bottom left spacing 1.5 font "Helvetica Bold,24"
unset key;
plot 'myoglobin_diffusion_comp.dat' u 1:7:8 ls 11 w yerrorbars title 'BD-HI',\
'myoglobin_diffusion_comp.dat' u 1:4:5 ls 13 w yerrorbars title 'BD-noHI',\
'myoglobin_diffusion_exp_and_toku.dat' u 1:2 ls 12 w p title 'Exp.',\
'myoglobin_diffusion_exp_and_toku.dat' u 1:3 ls 11 smooth bezier title 'Tokuyama';

set size 0.35,0.35
set origin 0.58,0.5
clear
set bmargin 1
set lmargin 1
set rmargin 1
set tmargin 1
set border lw 1

unset ylabel;
unset x2label;
unset x2tics;
set xrange [0.05:0.5];
set yrange [0.01:1.1];
set log x;
set log y;
set xtics ("0.1" 0.1,"0.2" 0.2, "0.3" 0.3, "0.4" 0.4) font "Helvetica Bold,20";
set xlabel "{/Symbol F}" font "Helvetica Bold,24" offset 0,0;
set ytics auto font "Helvetica Bold,20";
plot 'myoglobin_diffusion_comp.dat' u 1:7:8 ls 11 w yerrorbars title 'BD',\
'myoglobin_diffusion_comp.dat' u 1:4:5 ls 13 w yerrorbars title 'BD-noHI',\
'myoglobin_diffusion_exp_and_toku.dat' u 1:2 ls 12 w p title 'Exp.',\
'myoglobin_diffusion_exp_and_toku.dat' u 1:3 ls 11 smooth cspline title 'Tokuyama';

unset multiplot
unset log x;
unset log y;

###################################################################################################
###################################################################################################
# Hemoglobin
set size 1.0,1.0
set origin 0.0,0.0
set bmargin 4
set lmargin 12
set rmargin 4
set tmargin 4
set border lw 2

set output 'ps/hemoglobin_diffusion.ps';
set multiplot

unset title;
set xtics nomirror
set ytics nomirror
set xlabel "{/Symbol F}" font "Helvetica Bold,24" offset 0,-1;
set x2label "g/L" font "Helvetica Bold,24" offset 0, +1;
set ylabel "D^{L}_{s} ({/Helvetica=24 \305}^2/ns)" font "Helvetica Bold, 24" offset -2,+0;
set xtics 0.1 font "Helvetica Bold,24";
set x2tics auto font "Helvetica Bold,24";
set x2tics ("0" 0.0, "133" 0.1,"266" 0.2, "400" 0.3, "666" 0.4) font "Helvetica Bold,24";
set ytics auto font "Helvetica Bold,24";
set xrange [0.0:0.4];
set x2range [0.0:0.4];
set yrange [0.01:1.1];
set key bottom left spacing 1.5 font "Helvetica Bold,24"
unset key;
plot 'hemoglobin_diffusion_comp.dat' u 1:7:8 ls 11 w yerrorbars title 'BD-noHI',\
'' u 1:4:5 ls 13 w yerrorbars title 'BD-HI',\
'hemoglobin_diffusion_exp_and_toku.dat' u 1:2:3 ls 12 w yerrorbars title 'Exp.',\
'' u 1:5 ls 11 smooth bezier title 'Tokuyama';

set size 0.35,0.35
set origin 0.58,0.5
set bmargin 1
set lmargin 1
set rmargin 1
set tmargin 1
set border lw 1

unset ylabel;
unset x2label;
unset x2tics;
set xrange [0.02:0.4];
set yrange [0.1:1.1];
set log x;
set log y;
set xtics ("0.01" 0.01,  "0.1" 0.1,"0.2" 0.2, "0.3" 0.3) font "Helvetica Bold,20";
set ytics auto font "Helvetica Bold,20";
set xlabel "{/Symbol F}" font "Helvetica Bold,24" offset 0,0;
plot 'hemoglobin_diffusion_comp.dat' u 1:7:8 ls 11 w yerrorbars title 'BD-noHI',\
'' u 1:4:5 ls 13 w yerrorbars title 'BD-HI',\
'hemoglobin_diffusion_exp_and_toku.dat' u 1:2:3 ls 12 w yerrorbars title 'Exp.',\
'' u 1:5 ls 11 smooth cspline title 'Tokuyama';

unset multiplot;
set size 1.0,1.0;
unset log x;
unset log y;

# "Hemoglobin dissociation times";
set size 1.0,1.0
set origin 0.0,0.0
set bmargin 4.5
set lmargin 12
set rmargin 4
set tmargin 3
unset title;
set xlabel "Oligomer size" font "Helvetica,30" offset +0,-0.5;
set ylabel "{/Symbol t}_d (ns)" font "Helvetica,30" offset -2.5,0;
set key bottom right spacing 5.2 font "Helvetica Bold,30"
unset x2tics;
unset xtics;
unset log x;
unset log y;
set ytics auto font "Helvetica Bold,30";
set xtics ("1" 1,"2" 2, "3" 3, "4" 4) font "Helvetica Bold,30";
set xrange [1:5];
set yrange [5:40];
set output 'ps/hemoglobin_tau_off.ps';
set datafile missing "?";
plot 'hemoglobin_tau_off.dat'  u 1:2 w p ls 21 t "0.04",\
''  u 1:2 w l ls 21 notitle,\
''  u 3:4 w p ls 22 t "0.11",\
''  u 3:4 w l ls 22 notitle,\
''  u 5:6 w p ls 23 t "0.16",\
''  u 5:6 w l ls 23 notitle,\
''  u 7:8 w p ls 24 t "0.25",\
''  u 7:8 w l ls 24 notitle;




# --- Hemoglobin cluster histogram ---
set size 1.0,1.0
set origin 0.0,0.0
set bmargin 6
set lmargin 12
set rmargin 4
set tmargin 3

unset log x;
unset log y;
set title "Hemoglobin cluster histogram";
set notitle;
set xlabel "oligomer size" font "Helvetica,30" offset +0,-2;
#set x2label "{/Symbol F (%)}" font "Helvetica Bold,30";
set ylabel "Mole fraction ({/Symbol c}_i)" font "Helvetica,30" offset -2,0;
set key top right spacing 5.2 font "Helvetica Bold,30"
unset x2tics;
unset xtics;
set xtics ("1" 0, "2" 1, "3" 2, "4" 3, "5" 4);
#set x2tics ("1.12" 14, "2.22" 28, "3.37" 42, "4.49" 57) font "Helvetica Bold,30";
set ytics auto font "Helvetica Bold,30";
#set xtics auto font "Helvetica Bold,30";
set xrange [-1:5];
set yrange [0:1];
set boxwidth 0.9 absolute
#set style fill   solid 1.00 border -1
set style fill pattern border -1
set style histogram clustered gap 1 title  offset character 0, 0, 0
set datafile missing '-'
set style data histograms
set xtics border in scale 1,0.5 nomirror rotate by -45  offset character 0, -0.5, 0 font "Helvetica Bold,30"
set xtic rotate by 0;
set output 'ps/hemoglobin_oligomers4A_cutoff.ps';
plot 'hemoglobin_oligomers_4Acutoff.dat'  u 6  ls 1 t "0.04",\
''  u 12  ls 2 t "0.11",\
''  u 18 ls 3 t "0.16",\
''  u 24 ls 4 t "0.25";



# --- Hemoglobin n(r) fractal prop. ---
set size 1.0,1.0
set origin 0.0,0.0
set bmargin 4
set lmargin 12
set rmargin 4
set tmargin 3
FIT_LIMIT=1e-6

# "Hemoglobin fractal properties";
unset title;
set xlabel "r/2a" font "Helvetica,30" offset +0,-0;
set ylabel "n(r)" font "Helvetica,30" offset -0,0;
set key bottom right spacing 5.2 font "Helvetica Bold,30"
unset x2tics;
unset xtics;
set log x;
set log y;
set ytics auto font "Helvetica Bold,30";
set xtics ("1" 1,"2" 2, "3" 3) font "Helvetica Bold,30";
set xrange [1:3];
set yrange [0.01:10];
set output 'ps/hemoglobin_ncs.ps';
f1(x)=a*x**b
f2(x)=c*x**d
f3(x)=e*x**f
f4(x)=g*x**h
fit [200/(2*hemo_a):380/(2*hemo_a)] f1(x) "hemoglobin_ncs.dat" using ($1/(2*hemo_a)):2 via a,b;
fit [200/(2*hemo_a):250/(2*hemo_a)] f2(x) "hemoglobin_ncs.dat" using ($3/(2*hemo_a)):4 via c,d;
fit [150/(2*hemo_a):200/(2*hemo_a)] f3(x) "hemoglobin_ncs.dat" using ($5/(2*hemo_a)):6 via e,f;
fit [90/(2*hemo_a):120/(2*hemo_a)] f4(x) "hemoglobin_ncs.dat" using ($7/(2*hemo_a)):8 via g,h;
plot 'hemoglobin_ncs.dat'  u ($1/(2*hemo_a)):2 w p  ls 1 t "0.04 fit \a,\b",\
''  u ($3/(2*hemo_a)):4  w p ls 2 t "0.11",\
''  u ($5/(2*hemo_a)):6 w p ls 3 t "0.16",\
''  u ($7/(2*hemo_a)):8 w p ls 4 t "0.25",\
f1(x) w l ls 1 notitle,\
f2(x) w l ls 2 notitle,\
f3(x) w l ls 3 notitle,\
'' u (log($1)/log(s)):( (log($1)/log(s))>(log(800)/log(s)) ? 1/0 : f4(log($1)/log(s)) )  w l ls 23 notitle;
# A : B ? C
# Test A, if true execute B else execute C
# SEE HERE : http://t16web.lanl.gov/Kawano/gnuplot/misc1-e.html

